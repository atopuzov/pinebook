# U-Boot: Pine64
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=uboot-pine64
pkgver=2019.04
pkgrel=1
pkgdesc="U-Boot for Pine64"
uboot_ver=2019.04
arm_tf_ver=2.1
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
makedepends=('bc' 'git' 'python' 'swig' 'dtc')
install=${pkgname}.install
uboot_dir=u-boot-${uboot_ver}
arm_tf_dir=arm-trusted-firmware-${arm_tf_ver}

source=("https://github.com/u-boot/u-boot/archive/v${uboot_ver}.tar.gz"
        "https://github.com/ARM-software/arm-trusted-firmware/archive/v${arm_tf_ver}.tar.gz"
	'0001-ATF-set-fno-stack-protector.patch'
        'boot.txt'
        'mkscr')
md5sums=('8533e568393c2738421acd802a73a295'
         '75c8f4958fb493d9bd7a8e5a9636ec18'
         '3213c96ce0713249719dc73d58395620'
         '616ec8961a5304f3e03a4c115a3aed09'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd ${uboot_dir}

  cd ../${arm_tf_dir}
  patch -p1 < ../0001-ATF-set-fno-stack-protector.patch
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd ${arm_tf_dir}

  make PLAT=sun50i_a64 bl31
  cp build/sun50i_a64/release/bl31.bin ../${uboot_dir}

  cd ../${uboot_dir}

  make distclean
  make pine64_plus_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pine64.bin

  make sopine_baseboard_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-sopine.bin

  make pinebook_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinebook.bin
}

package() {
  cd ${uboot_dir}

  mkdir -p "${pkgdir}"/boot

  cp u-boot-sunxi-with-spl-{pine64,sopine,pinebook}.bin "${pkgdir}"/boot
  cp ../boot.txt ../mkscr "${pkgdir}"/boot
  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d "${pkgdir}"/boot/boot.txt "${pkgdir}"/boot/boot.scr
}
