# U-Boot: Pine64
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
#             Aleksandar Topuzovic <aleksandar.topuzovic@gmail.com>

buildarch=8

pkgbase=uboot-pine64
pkgname=('uboot-pinebook' 'uboot-sopine' 'uboot-pine64-plus')
pkgver=2019.04
pkgrel=1
pkgdesc="U-Boot for Pine64"
uboot_ver=2019.04
arm_tf_ver=2.1
arch=('aarch64')
install=${pkgbase}.install
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
makedepends=('bc' 'git' 'python' 'swig' 'dtc')
uboot_dir=u-boot-${uboot_ver}
arm_tf_dir=arm-trusted-firmware-${arm_tf_ver}

source=("https://github.com/u-boot/u-boot/archive/v${uboot_ver}.tar.gz"
        "https://github.com/ARM-software/arm-trusted-firmware/archive/v${arm_tf_ver}.tar.gz"
	      '0001-ATF-set-fno-stack-protector.patch'
        'boot.txt'
        'mkscr')
md5sums=('8533e568393c2738421acd802a73a295'
         '75c8f4958fb493d9bd7a8e5a9636ec18'
         '3213c96ce0713249719dc73d58395620'
         '616ec8961a5304f3e03a4c115a3aed09'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd ${uboot_dir}

  cd ../${arm_tf_dir}
  patch -p1 < ../0001-ATF-set-fno-stack-protector.patch
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd ${arm_tf_dir}

  make PLAT=sun50i_a64 bl31
  cp build/sun50i_a64/release/bl31.bin ../${uboot_dir}

  cd ../${uboot_dir}

  make distclean
  for i in pine64_plus sopine_baseboard pinebook; do
    msg ${i}
    make ${i}_defconfig
    echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
    make EXTRAVERSION=-${pkgrel}
    mkdir ${i}
    cat spl/sunxi-spl.bin u-boot.itb > ${i}/u-boot-sunxi-with-spl.bin
  done
}

common_package() {
  name=$1

  cd ${uboot_dir}

  install -d "${pkgdir}"/boot
  install -Dm644 ../boot.txt "${pkgdir}"/boot/boot.txt
  install -Dm755 ../mkscr "${pkgdir}"/boot/mkscr
  install -Dm644 ${name}/u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d "${pkgdir}"/boot/boot.txt "${pkgdir}"/boot/boot.scr
}

package_uboot-pine64-plus() {
  pkgdesc="U-Boot for Pine64+"
  # install=${pkgbase}.install

  common_package "pine64_plus"
}

package_uboot-sopine() {
  pkgdesc="U-Boot for Sopine"
  # install=${pkgbase}.install

  common_package "sopine_baseboard"
}
package_uboot-pinebook() {
  pkgdesc="U-Boot for Pinebook"
  # install=${pkgbase}.install

  common_package "pinebook"
}
